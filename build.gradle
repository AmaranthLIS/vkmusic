plugins {
    id 'java'
    id 'idea'
    id 'org.jetbrains.kotlin.jvm' version '1.2.61'
    id 'org.jetbrains.kotlin.kapt' version '1.2.61'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.2.61'
    id 'org.jetbrains.kotlin.plugin.jpa' version '1.2.61'
    id 'org.springframework.boot' version '2.0.4.RELEASE'
}

apply plugin: 'io.spring.dependency-management'

group = 'me.ruslanys'
version = '3.0.0-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

ext {
    kotlinVersion = '1.2.61'
    jacksonVersion = '2.9.6'
}

dependencies {
    // Spring
    compile('org.springframework.boot:spring-boot-starter')

    // Kotlin
    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    compile("org.jetbrains.kotlin:kotlin-reflect")
    compile('com.fasterxml.jackson.module:jackson-module-kotlin')

    // Jackson
    compile("com.fasterxml.jackson.core:jackson-databind")
    compile("com.fasterxml.jackson.core:jackson-annotations")
    compile("com.fasterxml.jackson.core:jackson-core")

    // Utils
    compile('org.reflections:reflections:0.9.11')
    compile('commons-codec:commons-codec:1.10')
    compile('org.apache.commons:commons-text:1.1')
    compile('org.apache.commons:commons-lang3:3.6')
    compile('com.google.guava:guava:23.0')
    compile('org.jsoup:jsoup:1.10.3')
    compile('com.mpatric:mp3agic:0.9.0')

    // DevTools
    kapt('org.springframework.boot:spring-boot-configuration-processor')

    // Tests
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
}

// Jar
jar {
    manifest {
        attributes("Implementation-Version": version)
    }
    archiveName = rootProject.name + ".jar"
}

bootJar {
    launchScript()
}

// Kotlin
compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}
noArg {
    annotations("javax.persistence.MappedSuperclass", "javax.persistence.Entity")
}

// IDEA
idea {
    module {
        def kaptMain = file('build/generated/source/kapt/main')
        sourceDirs += kaptMain
        generatedSourceDirs += kaptMain
    }
}